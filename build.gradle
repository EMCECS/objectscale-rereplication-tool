plugins {
    id 'idea'
    id 'eclipse'
    id 'java'
    id 'io.freefair.lombok' version '5.3.0'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'com.github.jk1.dependency-license-report' version '1.17'
}

group = 'com.dellemc.objectscale'
version = '1.2'

mainClassName = "com.dellemc.objectscale.tool.ReReplicationCli"
archivesBaseName = 'rereplication-tool'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

configurations {
    // need a separate scope for the shadow app, for logging config
    shadow.extendsFrom runtimeClasspath
}

dependencies {
    implementation 'org.apache.logging.log4j:log4j-core:2.14.1'
    implementation platform('software.amazon.awssdk:bom:2.17.24')
    implementation 'software.amazon.awssdk:s3'
    implementation 'software.amazon.awssdk:apache-client'
    implementation 'commons-cli:commons-cli:1.4'
    implementation 'org.apache.commons:commons-csv:1.8'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.2'
    shadow 'org.apache.logging.log4j:log4j-slf4j-impl:2.14.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.2'
    testRuntimeOnly 'org.apache.logging.log4j:log4j-slf4j-impl:2.14.1'
}

shadowJar {
    classifier ''
    configurations = [project.configurations.shadow]
    manifest {
        attributes 'Implementation-Version': project.version
    }
    from('src/app/resources')
}

test {
    useJUnitPlatform()
}

import com.github.jk1.license.render.*
licenseReport {
    renderers = [new InventoryHtmlReportRenderer(), new CsvReportRenderer()]
}
